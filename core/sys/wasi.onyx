package system

#load "core/wasi"

use package wasi
use package core
use package main as main

STDOUT_FILENO :: 1

output_str :: proc (s: str) -> u32 {
    vec := IOVec.{ buf = s.data, len = s.count };
    tmp : Size;
    fd_write(STDOUT_FILENO, IOVecArray.{ ^vec, 1 }, ^tmp);
    fd_datasync(STDOUT_FILENO);
    return tmp;
}

assert_handler :: proc (msg: str, file: str) {
    output_str("Assert failed: ");
    output_str(msg);

    if file.data != null {
        output_str(" in ");
        output_str(file);
    }

    proc_exit(1);
}

// The builtin _start proc.
// Sets up everything needed for execution.
proc () #export "_start" {
    alloc.init();

    context.allocator = alloc.heap_allocator;
    context.temp_allocator = alloc.heap_allocator;
    context.assert_handler = assert_handler;

    argc : Size;
    argv_buf_size : Size;

    args_sizes_get(^argc, ^argv_buf_size);

    argv := cast(^cstr) calloc(sizeof cstr * argc);
    argv_buf := cast(cstr) calloc(argv_buf_size);

    args_get(argv, argv_buf);

    stdio_init();

    main.main(argv[0 .. argc]);

    print_buffer_flush();
}
