package runtime.platform

use core {package, *}
use runtime
use runtime {
    __runtime_initialize,
    Multi_Threading_Enabled,
}

use main

// Platform supports
Supports_Files :: false
Supports_Directories :: false
Supports_Os :: false
Supports_Processes :: false
Supports_Time :: false
Supports_Networking :: false
Supports_Type_Info :: true
Supports_Threads :: true

__output_string   :: (s: str)      -> u32  #foreign "host" "print_str" ---
__output_error    :: (s: str)      -> u32  #foreign "host" "print_str" ---
__exit            :: (status: i32) -> void #foreign "host" "exit" ---
__read_from_input :: (buf: [] u8)  -> u32 do return 0;

// Sets up everything needed for execution.
__start :: () {
    __runtime_initialize();
    context.thread_id = 0;

    #if (typeof main.main) == #type () -> void {
        main.main();

    } else {
        args: [] cstr = .{ null, 0 };
        main.main(args);
    }

    __flush_stdio();
}

#if Multi_Threading_Enabled {
    __spawn_thread :: (id: i32, tls_base: rawptr, func: (data: rawptr) -> void, data: rawptr) -> bool #foreign "host" "spawn_thread" ---
    __kill_thread  :: (id: i32) -> i32 #foreign "host" "kill_thread" ---

    #export "_thread_start" runtime._thread_start
    #export "_thread_exit"  runtime._thread_exit
}
