package runtime

#load "core/runtime/common"

use package core

__output_string   :: (s: str)      -> u32  #foreign "host" "print_str" ---
__exit            :: (status: i32) -> void #foreign "host" "exit" ---
__read_from_input :: (buf: [] u8)  -> u32 do return 0;

// The builtin _start proc.
// Sets up everything needed for execution.
#export "_start" () {
    __runtime_initialize();
    context.thread_id = 0;

    #if (typeof (package main).main) == #type () -> void {
        (package main).main();

    } else {
        args: [] cstr = .{ null, 0 };
        (package main).main(args);
    }

    __flush_stdio();
}

#if Multi_Threading_Enabled {
    __spawn_thread :: (id: i32, tls_base: rawptr, func: (data: rawptr) -> void, data: rawptr) -> bool #foreign "host" "spawn_thread" ---
    __kill_thread  :: (id: i32) -> i32 #foreign "host" "kill_thread" ---

    #export "_thread_start" _thread_start
    #export "_thread_exit"  _thread_exit
}
