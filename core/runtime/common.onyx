package runtime

use package core
use package core.intrinsics.onyx { __initialize }

// The default assert handler. This assumes that __output_string
// and __exit are defined in the 'runtime' package.
__assert_handler :: (msg: str, site: CallSite) {
    __output_string("Assert failed: ");
    __output_string(msg);

    if site.file.data != null {
        __output_string(" in ");
        __output_string(site.file);
    }

    __output_string("\n");

    __exit(1);
}

__runtime_initialize :: () {
    __initialize_data_segments();

    alloc.init();
    __thread_initialize();

    #if Multi_Threading_Enabled do thread.__initialize();
}

// Use this procedure to initialize everything needed in the
// standard library when you are dropped directly into a function.
__thread_initialize :: () {
    __tls_base = raw_alloc(alloc.heap_allocator, __tls_size);
    memory.set(__tls_base, 0, __tls_size);

    __initialize(^context);
    context.allocator = alloc.heap_allocator;
    context.temp_allocator = alloc.temp_allocator;
    context.assert_handler = __assert_handler;

    __stdio_init();
}

