package core.iter

#local memory :: package core.memory
#local alloc  :: package core.alloc

#local {
    //
    // After struggling to think of a good way for iterators to manage their context
    // data in a way that is completely degradative to the heap memory, I decided that
    // the easiest and most programmer friendly way to do it was to completely remove
    // their dependence on the heap. ALL iterators allocated in this file use the
    // buffer below to store their data. This data is never "freed", but because it
    // is a ring buffer, the data will be reused eventually. I think that 1024 bytes
    // should be plently for the normal number of active iterators. However, it is
    // possible that this *could* overrun, and unexpected behavior could ensue. For
    // this reason, most allocator also support allocating their data from the context's
    // allocator simply by passing that allocator as an arugment.

    iter_ring_buffer: [1024] u8;
    iter_ring: alloc.ring.RingState;
    iter_allocator: Allocator;

    #init () {
        iter_ring = alloc.ring.make(iter_ring_buffer);
        iter_allocator = alloc.as_allocator(^iter_ring);
    }
}

as_iterator :: #match {}

Iterable :: interface (t: $T) {
    { as_iterator(t) } -> Iterator;
}

close :: (it: Iterator($T)) {
    it.close(it.data);
}

filter :: #match #local {}
#overload
filter :: (it: Iterator($T), predicate: (T) -> bool, allocator := iter_allocator) -> Iterator(T) {
    FilterIterator :: struct (T: type_expr) {
        iterator:  Iterator(T);
        predicate: (T) -> bool;
        allocator: Allocator;
    }

    filter_iterator := new(FilterIterator(T), allocator=allocator);
    filter_iterator.iterator = it;
    filter_iterator.predicate = predicate;
    filter_iterator.allocator = allocator;

    next :: (fi: ^FilterIterator($T)) -> (T, bool) {
        value, cont := fi.iterator.next(fi.iterator.data);
        if cont {
            while !fi.predicate(value) {
                value, cont = fi.iterator.next(fi.iterator.data);
                if !cont do return value, false;
            }

            return value, true;
        } else {
            return value, false;
        }
    }

    close :: (fi: ^FilterIterator($T)) {
        if fi.iterator.close != null_proc do fi.iterator.close(fi.iterator.data);
        raw_free(fi.allocator, fi);
    }

    return .{
        data  = filter_iterator,
        next  = #solidify next  { T=T },
        close = #solidify close { T=T },
    };
}

#overload
filter :: (it: Iterator($T), ctx: $Ctx, predicate: (T, Ctx) -> bool, allocator := iter_allocator) -> Iterator(T) {
    FilterIterator :: struct (T: type_expr, Ctx: type_expr) {
        iterator:  Iterator(T);
        predicate: (T, Ctx) -> bool;
        ctx: Ctx;
        allocator: Allocator;
    }

    filter_iterator := new(FilterIterator(T, Ctx), allocator=allocator);
    filter_iterator.iterator = it;
    filter_iterator.predicate = predicate;
    filter_iterator.ctx = ctx;
    filter_iterator.allocator = allocator;

    next :: (fi: ^FilterIterator($T, $_)) -> (T, bool) {
        value, cont := fi.iterator.next(fi.iterator.data);
        if cont {
            while !fi.predicate(value, fi.ctx) {
                value, cont = fi.iterator.next(fi.iterator.data);
                if !cont do return value, false;
            }

            return value, true;
        } else {
            return value, false;
        }
    }

    close :: (fi: ^FilterIterator($T, $_)) {
        if fi.iterator.close != null_proc do fi.iterator.close(fi.iterator.data);
        raw_free(fi.allocator, fi);
    }

    return .{
        data  = filter_iterator,
        next  = #solidify next  { T=T, _=Ctx },
        close = #solidify close { T=T, _=Ctx },
    };
}

map :: #match #local {}

#overload
map :: (it: Iterator($T), transform: (T) -> $R, allocator := iter_allocator) -> Iterator(R) {
    MapIterator :: struct (T: type_expr, R: type_expr) {
        iterator:  Iterator(T);
        transform: (T) -> R;
        allocator: Allocator;
    }

    map_iterator := new(MapIterator(T, R), allocator=allocator);
    map_iterator.iterator = it;
    map_iterator.transform = transform;
    map_iterator.allocator = allocator;

    next :: (mi: ^MapIterator($T, $R)) -> (R, bool) {
        value, cont := mi.iterator.next(mi.iterator.data);
        if !cont do return .{}, false;

        return mi.transform(value), true;
    }

    close :: (mi: ^MapIterator($T, $R)) {
        if mi.iterator.close != null_proc do mi.iterator.close(mi.iterator.data);
        raw_free(mi.allocator, mi);
    }

    return .{
        data  = map_iterator,
        next  = #solidify next  { T=T, R=R },
        close = #solidify close { T=T, R=R },
    };
}

#overload
map :: (it: Iterator($T), ctx: $Ctx, transform: (T, Ctx) -> $R, allocator := iter_allocator) -> Iterator(R) {
    MapIterator :: struct (T: type_expr, R: type_expr, Ctx: type_expr) {
        iterator:  Iterator(T);
        transform: (T, Ctx) -> R;
        ctx: Ctx;
        allocator: Allocator;
    }

    map_iterator := new(MapIterator(T, R, Ctx), allocator=allocator);
    map_iterator.iterator = it;
    map_iterator.transform = transform;
    map_iterator.ctx = ctx;
    map_iterator.allocator = allocator;

    next :: (mi: ^MapIterator($T, $R, $Ctx)) -> (R, bool) {
        value, cont := mi.iterator.next(mi.iterator.data);
        if !cont do return .{}, false;

        return mi.transform(value, mi.ctx), true;
    }

    close :: (mi: ^MapIterator($T, $R, $Ctx)) {
        if mi.iterator.close != null_proc do mi.iterator.close(mi.iterator.data);
        raw_free(mi.allocator, mi);
    }

    return .{
        data  = map_iterator,
        next  = #solidify next  { T=T, R=R, Ctx=Ctx },
        close = #solidify close { T=T, R=R, Ctx=Ctx },
    };
}

take_one :: (it: Iterator($T), no_close := false) -> (T, bool) {
    ret, cont := it.next(it.data);
    if !cont && !no_close {
        if it.close != null_proc do it.close(it.data);
    }
    return ret, cont;
}

// Macro that allows you to extract elements from an iterator in a simple way:
//
// value: i32;
// iterator: Iterator(i32) = ...;
//
// if #(value) << iterator {
//     ...iterater closed...
// }
#operator << macro (dest: Code, it: Iterator($T)) -> bool {
    take_one :: take_one

    cont: bool;
    (#unquote dest), cont = take_one(it);
    return !cont;
}

take :: (it: Iterator($T), count: u32, allocator := iter_allocator) -> Iterator(T) {
    TakeIterator :: struct (T: type_expr) {
        iterator:  Iterator(T);
        remaining: u32;
        allocator: Allocator;
    }

    take_iterator := new(TakeIterator(T), allocator=allocator);
    take_iterator.iterator = it;
    take_iterator.remaining = count;
    take_iterator.allocator = allocator;

    next :: ($T: type_expr, ti: ^TakeIterator(T)) -> (T, bool) {
        if ti.remaining == 0 do return .{}, false;

        ti.remaining -= 1;
        return ti.iterator.next(ti.iterator.data);
    }

    close :: ($T: type_expr, ti: ^TakeIterator(T)) {
        ti.iterator.close(ti.iterator.data);
        raw_free(ti.allocator, ti);
    }

    return .{
        data  = take_iterator,
        next  = #solidify next  { T=T },
        close = #solidify close { T=T },
    };
}

take_while :: (it: Iterator($T), predicate: (T) -> bool, allocator := iter_allocator) -> Iterator(T) {
    TakeIterator :: struct (T: type_expr) {
        iterator:  Iterator(T);
        predicate: (T) -> bool;
        allocator: Allocator;
    }

    take_iterator := new(TakeIterator(T), allocator=allocator);
    take_iterator.iterator = it;
    take_iterator.predicate = predicate;
    take_iterator.allocator = allocator;

    next :: ($T: type_expr, ti: ^TakeIterator(T)) -> (T, bool) {
        value, cont := ti.iterator.next(ti.iterator.data);
        if !cont do return value, false;

        return value, ti.predicate(value);
    }

    close :: ($T: type_expr, ti: ^TakeIterator(T)) {
        if ti.iterator.close != null_proc do ti.iterator.close(ti.iterator.data);
        raw_free(ti.allocator, ti);
    }

    return .{
        data  = take_iterator,
        next  = #solidify next  { T=T },
        close = #solidify close { T=T },
    };
}

skip :: (it: Iterator($T), count: u32, allocator := iter_allocator) -> Iterator(T) {
    SkipIterator :: struct (T: type_expr) {
        iterator: Iterator(T);
        to_skip:  i32;
        skipped:  bool = false;
        allocator: Allocator;
    }

    skip_iterator := new(SkipIterator(T), allocator=allocator);
    skip_iterator.iterator = it;
    skip_iterator.to_skip = count;
    skip_iterator.allocator = allocator;

    next :: (si: ^SkipIterator($T)) -> (T, bool) {
        while !si.skipped && si.to_skip > 0 {
            si.to_skip -= 1;
            value, cont := si.iterator.next(si.iterator.data);

            if !cont {
                si.skipped = true;
                return value, false;
            }
        }

        return si.iterator.next(si.iterator.data);
    }

    close :: (si: ^SkipIterator($T)) {
        if si.iterator.close != null_proc do si.iterator.close(si.iterator.data);
        raw_free(si.allocator, si);
    }

    return .{
        data  = skip_iterator,
        next  = #solidify next  { T=T },
        close = #solidify close { T=T },
    };
}

#local Zipped :: struct (T: type_expr, R: type_expr) {
    first: T;
    second: R;
}

zip :: (left_iterator: Iterator($T), right_iterator: Iterator($R), allocator := iter_allocator) -> Iterator(Zipped(T, R)) {
    ZippedIterator :: struct (T: type_expr, R: type_expr) {
        iterator1: Iterator(T);
        iterator2: Iterator(R);
        allocator: Allocator;
    }

    zipped_iterator := new(ZippedIterator(T, R), allocator=allocator);
    zipped_iterator.iterator1 = left_iterator;
    zipped_iterator.iterator2 = right_iterator;
    zipped_iterator.allocator = allocator;

    next :: (zi: ^ZippedIterator($T, $R)) -> (Zipped(T, R), bool) {
        v1, cont1 := zi.iterator1.next(zi.iterator1.data);
        v2, cont2 := zi.iterator2.next(zi.iterator2.data);

        return .{ v1, v2 }, cont1 && cont2;
    }

    close :: (zi: ^ZippedIterator($T, $R)) {
        if zi.iterator1.close != null_proc do zi.iterator1.close(zi.iterator1.data);
        if zi.iterator2.close != null_proc do zi.iterator2.close(zi.iterator2.data);
        raw_free(zi.allocator, zi);
    }

    return .{
        data  = zipped_iterator,
        next  = #solidify next  { T=T, R=R },
        close = #solidify close { T=T, R=R },
    };
}

concat :: (iters: ..Iterator($T)) -> Iterator(T) {
    Context :: struct (T: type_expr) {
        iters: [] Iterator(T);
        idx:   u32;
    }

    c := new(Context(T), allocator=iter_allocator);
    c.iters = memory.copy_slice(iters, allocator=iter_allocator);
    c.idx = 0;

    next :: (use c: ^Context($T)) -> (T, bool) {
        while true {
            if idx >= iters.count do return .{}, false;        

            curr_iter := ^iters[idx];
            value, valid := curr_iter.next(curr_iter.data);
            if valid {
                return value, true;
            }

            idx += 1;
        }
    }

    close :: (use c: ^Context($T)) {
        // I don't feel like this should always close ALL the iterators...
        // But I don't know what the semantics should be for specifying which
        // if any iterators to close.
        for^ iters {
            it.close(it.data);
        }

        // memory.free_slice(^iters);
        // cfree(c);
    }

    return .{
        c,
        #solidify next {T=T},
        #solidify close {T=T}
    };
}

const :: (value: $T) -> Iterator(T) {
    next :: (data: ^$T) -> (T, bool) {
        return *(cast(^T) data), true;
    }

    allocated := cast(^T) raw_alloc(iter_allocator, sizeof T);
    *allocated = value;

    return .{
        data = allocated,
        next = #solidify next { T=T },
    };
}

#local Enumeration_Value :: struct (T: type_expr) {
    index: i32;
    value: T;
}

enumerate :: #match #local {}

#overload
enumerate :: macro (it: $T/Iterable, start_index: i32 = 0) => {
    as_iterator :: as_iterator
    enumerate :: enumerate
    return enumerate(as_iterator(it), start_index);
}

#overload
enumerate :: (it: Iterator($T), start_index: i32 = 0) -> Iterator(Enumeration_Value(T)) {
    Enumeration_Context :: struct (T: type_expr) {
        iterator:      Iterator(T);
        current_index: i32;
    }

    ec := make(Enumeration_Context(T), allocator=iter_allocator);
    ec.iterator = it;
    ec.current_index = start_index;

    next :: (use data: ^Enumeration_Context($T)) -> (Enumeration_Value(T), bool) {
        value, cont := iterator.next(iterator.data);

        if !cont do return .{ current_index, .{} }, false;

        defer current_index += 1;
        return .{ current_index, value }, true;
    }

    close :: (use data: ^Enumeration_Context($T)) {
        if iterator.close != null_proc do iterator.close(iterator.data);
        // cfree(data);
    }

    return .{
        data  = ec,
        next  = #solidify next  { T = T },
        close = #solidify close { T = T },
    };
}

#overload
as_iterator :: from_array
from_array :: (arr: [] $T) -> Iterator(^T) {
    Context :: struct (T: type_expr) {
        data: ^T;
        count: u32;
        current: u32;
    }

    c := make(Context(T), allocator=iter_allocator);
    c.data = arr.data;
    c.count = arr.count;
    c.current = 0;

    next :: (use _: ^Context($T)) -> (^T, bool) {
        if current < count {
            defer current += 1;
            return ^data[current], true;

        } else {
            return null, false;
        }
    }

    return .{
        data  = c,
        next  = #solidify next { T = T },
    };
}

#overload
as_iterator :: (x: ^[..] $T) -> Iterator(T) {
    Context :: struct (T: type_expr) {
        arr: ^[..] T;
        current: u32;
    }

    c := make(Context(T), allocator=iter_allocator);
    c.arr = x;
    c.current = 0;

    next :: (use _: ^Context($T)) -> (T, bool) {
        if current < arr.count {
            defer current += 1;
            return arr.data[current], true;

        } else {
            return .{}, false;
        }
    }

    remove :: (use _: ^Context($T)) {
        //
        // This is current - 1 because current will have already
        // been incremented by the time this element calls #remove.
        array :: package core.array
        array.delete(arr, current - 1);
        current -= 1;
    }

    return .{
        data  = c,
        next  = #solidify next { T = T },
        remove = #solidify remove { T = T },
    };
}

#overload
as_iterator :: (x: ^[..] $T, by_pointer: bool) -> Iterator(^T) {
    Context :: struct (T: type_expr) {
        arr: ^[..] T;
        current: u32;
    }

    c := make(Context(T), allocator=iter_allocator);
    c.arr = x;
    c.current = 0;

    next :: (use _: ^Context($T)) -> (^T, bool) {
        if current < arr.count {
            defer current += 1;
            return ^arr.data[current], true;

        } else {
            return null, false;
        }
    }

    remove :: (use _: ^Context($T)) {
        //
        // This is current - 1 because current will have already
        // been incremented by the time this element calls #remove.
        array :: package core.array
        array.delete(arr, current - 1);
        current -= 1;
    }

    return .{
        data  = c,
        next  = #solidify next { T = T },
        remove = #solidify remove { T = T },
    };
}

#overload
as_iterator :: (r: range) -> Iterator(i32) {
    Context :: struct {
        r: range;
        v: i32;
    }

    next :: (use c: ^Context) -> (i32, bool) {
        if r.step > 0 {
            if v >= r.high {
                return 0, false;
            } else {
                defer v += r.step;
                return v, true;
            }

        } else {
            if v < r.high {
                return 0, false;
            } else {
                defer v += r.step;
                return v, true;
            }
        }
    }

    c := new(Context, allocator=iter_allocator);
    c.r = r;
    c.v = r.low;

    return .{
        data  = c,
        next  = next,
    };
}

fold :: #match #local {}

// @Cleanup // some way to shorten this would be nice
#overload
fold :: macro (it: $T/Iterable, init: $R, combine: $S) => {
    fold :: fold
    return fold(as_iterator(it), init, combine);
}

#overload
fold :: (it: Iterator($T), initial_value: $R, combine: (T, R) -> R) -> R {
    for value: it {
        initial_value = combine(value, initial_value);
    }

    return initial_value;
}

count :: #match #local {}

#overload
count :: macro (it: $T/Iterable, cond: $F) => {
    count :: count
    return count(as_iterator(it), cond);
}

#overload
count :: (it: Iterator($T), cond: (T) -> bool) -> i32 {
    c := 0;
    for value: it do if cond(value) do c += 1;
    return c;
}

some :: #match #local {}

#overload
some :: macro (it: $T/Iterable, cond: $F) => {
    some :: some
    as_iterator :: as_iterator
    return some(as_iterator(it), cond);
}

#overload
some :: (it: Iterator($T), cond: (T) -> bool) -> bool {
    for value: it do if cond(value) do return true;
    return false;
}

every :: #match #local {}

#overload
every :: macro (it: $T/Iterable, cond: $F) => {
    every :: every
    as_iterator :: as_iterator
    return every(as_iterator(it), cond);
}

#overload
every :: (it: Iterator($T), cond: (T) -> bool) -> bool {
    for value: it do if !cond(value) do return false;
    return true;
}

to_array :: (it: Iterator($T), allocator := context.allocator) -> [..] T {
    array :: package core.array

    arr := array.make(T, allocator=allocator);
    for v: it do array.push(^arr, v);

    return arr;
}


#if runtime.Multi_Threading_Enabled {
    #local sync :: core.sync

    distributor :: #match #local {}

    #overload
    distributor :: macro (it: $T/Iterable) => {
        distributor :: distributor;
        as_iterator :: as_iterator;
        return distributor(as_iterator(it));
    }

    #overload
    distributor :: (it: Iterator) -> Iterator(it.Iter_Type) {
        Context :: struct (T: type_expr) {
            mutex: sync.Mutex;
            iterator: Iterator(T);
            ended := false;
        }

        next :: (use c: ^Context($T)) -> (T, bool) {
            if ended do return .{}, false;
            sync.scoped_mutex(^mutex);

            if v, success := take_one(iterator); !success {
                ended = true;
                return v, false;
            } else {
                return v, true;
            }
        }

        close :: (use c: ^Context($T)) {
            sync.mutex_destroy(^c.mutex);
            cfree(c);
        }

        // This iterator's context is allocated from the heap because
        // generally, a distributor iterator will be used across theads
        // in parallel programs. Programs such as those *might* make
        // a lot of iterators in their theads and I don't want to cause
        // the distributor's context be overwritten.
        c := new(Context(it.Iter_Type));
        sync.mutex_init(^c.mutex);
        c.iterator = it;

        return .{c, #solidify next {T=it.Iter_Type}, #solidify close {T=it.Iter_Type}};
    }

    parallel_for :: #match #local {}

    #overload
    parallel_for :: macro (iterable: $I/Iterable, thread_count: u32, thread_data: ^$Ctx, body: Code) {
        parallel_for :: parallel_for;
        as_iterator  :: as_iterator;

        parallel_for(as_iterator(iterable), thread_count, thread_data, body);
    }

    #overload
    parallel_for :: macro (iter: Iterator($T), thread_count: u32, thread_data: ^$Ctx, body: Code) {
        thread :: package core.thread;
        alloc  :: package core.alloc;
        distributor :: distributor;
        as_iterator :: as_iterator;

        if thread_count != 0 {
            dist := distributor(iter);
            t_data := Thread_Data(iter.Iter_Type, Ctx).{
                iter = ^dist,
                data = thread_data,
            };

            threads := alloc.array_from_stack(thread.Thread, thread_count - 1);
            for^ threads do thread.spawn(it, ^t_data, #solidify thread_function {body=body});

            thread_function(^t_data, body);

            for^ threads do thread.join(it);
            dist.close(dist.data);
        }

        Thread_Data :: struct (T: type_expr, Ctx: type_expr) {
            iter: ^Iterator(T);
            data: ^Ctx;
        }

        thread_function :: (__data: ^Thread_Data, $body: Code) {
            thread_data := __data.data;
            for #no_close *__data.iter {
                #unquote body;
            }
        }
    }
}
